### 第8章 数据处理的两个基本问题
#### 8.1 bx、si、di和bp
    1>  在8086CPU中，只有bx,si,di,bp可以在[]中来进行内存单元的寻址
    2>  4个寄存器可以单个出现,或是以以下组合出现:
    bx和si  bx和di  bp和si   bp和di
    3>  在[]中使用寄存器bp，若指令没显性的给出段地址,默认为ss 
#### 8.2 机器指令处理数据在什么地方 
#### 8.3 汇编语言中数据位置的表达
    1>立即数(idata)
    2>寄存器
    3>段地址(SA)和偏移地址(EA)
#### 8.4 寻址方式 
    8086CPU有多种寻址方式,总结如下 

**直接寻址**

    [idata]                 EA=idata;SA=(ds)

**寄存器间接寻址**

    [bx]                    EA=(bx);SA=(ds)
    [di]                    EA=(di);SA=(ds)
    [si]                    EA=(si);SA=(ds)
    [bp]                    EA=(bp);SA=(ss)         
    
**寄存器相对寻址**

    [bx+idata]              EA=(bx)+idata;SA=(ds)
    [si+idata]              EA=(si)+idata;SA=(ds)
    [di+idata]              EA=(di)+idata;SA=(ds)
    [bp+idata]              EA=(bp)+idata;SA=(ss)

**基址变址寻址**
    
    [bx+si]                 EA=(bx)+(si);SA=(ds)
    [bx+di]                 EA=(bx)+(di);SA=(ds)
    [bp+si]                 EA=(bp)+(si);SA=(ss)
    [bp+si]                 EA=(bp)+(si);SA=(ss)

**相对基址变址寻址**

    [bx+si+idata]                 EA=(bx)+(si)+idata;SA=(ds)
    [bx+di+idata]                 EA=(bx)+(di)+idata;SA=(ds)
    [bp+si+idata]                 EA=(bp)+(si)+idata;SA=(ss)
    [bp+si+idata]                 EA=(bp)+(si)+idata;SA=(ss)

#### 8.5 指令要处理的数据有多长
    1> 通过寄存器名得到要处理的数据的尺寸
       mov ax,1
       mov al,1
    2> 在没有寄存器名存在的情况下，用操作符 X ptr指明内存单元的长度
       mov word ptr ds:[0], 1
       mov byte ptr ds:[0], 1
    3> 其他
       push指令只进行字操作

#### 8.6 寻址方式的综合应用
    mov ax,seg
    mov ds,ax
    mov bx,60h

    mov word ptr [bx+0ch],38 
    add word ptr [bx+0eh],70
    mov si,0
    mov byte ptr [bx+10h+si],'V'
    inc si
    mov byte ptr [bx+10h+si],'A'
    inc si 
    mov byte ptr [bx+10h+si],'X'
    一般来说，我们可以用[bx+idata+si]的方式来访问结构体的数据.
    bx定位结构体，idata定位结构项,si定位每个元素

#### 8.7 div指令
    div byte ptr ds:[0]
    (al) = (ax) /((ds)*16 + 0) 的商
    (ah) = (ax) /((ds)*16 + 0) 的余数 

    div word ptr es:[0]
    (ax) = [(dx) * 10000h + (ax)] /((es) * 16 + 0) 的商
    (dx) = [(dx) * 10000h + (ax)] /((es) * 16 + 0) 的余数

    div byte ptr [bx+si+8]
    (al) = (ax) /((ds) * 16 + (bx) + (si) + 8) 的商
    (ah) = (ax) /((ds) * 16 + (bx) + (si) + 8) 的余数

    div word ptr [bx+si+8]
    (ax) = [(dx)*10000h + (ax)] /((ds) * 16 + (bx) + (si) + 8) 的商 
    (dx) = [(dx)*10000h + (ax)] /((ds) * 16 + (bx) + (si) + 8) 的余数

#### 伪指令dd 
    db 字节型数据  dw 字型数据 dd 双字型数据
    data segment 
        dd 100001
        dw 100
        dw 0 
    data ends 
    用第一个数除以第二个数，商存放在第三个数 
    mov ax,data 
    mov ds,ax 
    mov ax,ds:[0]
    mov dx,ds:[2]
    div word ptr ds:[4]
    mov ds:[6],ax

#### 8.9 dup 
    用于数据的重复
    db 3 dup (0,1,2)
    相当于 db 0,1,2,0,1,2,0,1,2

### 实验7 寻址方式在结构化数据访问中的应用 
    ;王爽汇编语言实验七
    assume cs:code,ds:data,es:table

    data segment
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
    db '1993','1994','1995' 
    ;以上是表示21年的字符串 4 * 21 = 84

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
    ;以上是表示21年公司总收入的dword型数据 4 * 21 = 84

    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
    dw 11542,14430,15257,17800
    ;以上是表示21年公司雇员人数的21个word型数据 2 * 21 = 42
    data ends

    table segment
    db 21 dup ('year summ ne ?? ') ; 'year summ ne ?? ' 刚好16个字节
    table ends

    code segment
    start:
    ;data数据 放在ds
    mov ax,data
    mov ds,ax
    ;table数据 放在 es
    mov ax,table
    mov es,ax
    ;三个寄存器初始化
    mov bx,0 ;data中数据定位（和idata结合，用于年份和收入）
    mov si,0 ;table中定位（和idata给合用于定位存放数据的相对位置）
    mov di,0 ;data中用于得到员工数
    ;cx循环次数
    mov cx,21
    ;循环内容
    s:
        ;年
        ;将年从data 到 table 分为高16位和低16位
        mov ax,[bx]
        mov es:[si],ax
        mov ax,[bx+2]
        mov es:[si+2],ax

        ;空格
        ;table 增加空格
        mov byte ptr es:[si+4],20h ;  mov BYTE PTR [DI],0就是把[DI]处的一个字节填为空格

        ;收入
        ;将收入从data 到 table 分为高16位和低16位
        mov ax,[bx+84]
        mov es:[si+5],ax
        mov ax,[bx+86]
        mov es:[si+7],ax

        ;空格
        ;table 增加空格
        mov byte ptr es:[si+ 9],20h

        ;雇员数
        ;将雇员数从data 到 table 
        mov ax,[di + 168]
        mov es:[si + 10],ax

        ;空格
        ;table 增加空格
        mov byte ptr es:[si+12],20h

        ;计算工资
        ;取ds处工资,32位
        mov ax,[bx + 84]
        mov dx,[bx + 86]
        ;计算人均收入
        div word ptr ds:[di + 168]
        ;将结果存入table处
        mov es:[si+13],ax

        ;空格
        ;table 增加空格
        mov byte ptr es:[si + 0fh],20h

        ;改变三个寄存器值
        add si,16
        add di,2
        add bx,4

    loop s

    mov ax,4ch
    int 21h
    code ends
    end start





    
